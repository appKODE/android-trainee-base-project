buildscript {
  repositories {
    google()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2" }
  }
}

plugins {
  alias(libs.plugins.android.application) apply false
  alias(libs.plugins.kotlin.android) apply false
  alias(libs.plugins.kotlin.kapt) apply false
  alias(libs.plugins.kotlin.parcelize) apply false
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

configurations {
  ktlint
}

dependencies {
  ktlint(libs.ktlint) {
    // this is required due to https://github.com/pinterest/ktlint/issues/1114
    attributes {
      attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

tasks.register("ktlint", JavaExec) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  description = "Check Kotlin code style and format"
  classpath = configurations.ktlint
  mainClass = "com.pinterest.ktlint.Main"
  jvmArgs("--add-opens=java.base/java.lang=ALL-UNNAMED")
  // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
  args(
    "--editorconfig=.editorconfig",
    "**/src/**/*.kt",
    "**.kts",
    "!**/build/**",
  )
}

tasks.register("ktlintFormat", JavaExec) {
  group = LifecycleBasePlugin.VERIFICATION_GROUP
  description = "Fix Kotlin code style deviations"
  classpath = configurations.ktlint
  mainClass = "com.pinterest.ktlint.Main"
  jvmArgs("--add-opens=java.base/java.lang=ALL-UNNAMED")
  // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
  args(
    "-F",
    "--editorconfig=.editorconfig",
    "**/src/**/*.kt",
    "**.kts",
    "!**/build/**",
  )
}