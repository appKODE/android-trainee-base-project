plugins {
  id "io.gitlab.arturbosch.detekt"
}

dependencies {
  detektPlugins(libs.detekt.rules.compose)
  detektPlugins(files(rootProject.file("lib/detekt-rules-1.4.0.jar")))
}

detekt {
  config = files(rootProject.file("detekt-config.yml"))
  ignoredBuildTypes = ["release"]
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
  jvmTarget = "17"
  // "exclude(**/generated/**)" doesn't work for some reason, see
  // https://github.com/detekt/detekt/issues/4127#issuecomment-1260733842
  exclude {
    // Use separator to make sure it also works on Windows
    def sep = File.separator
    it.file.absolutePath.contains("${sep}generated${sep}")
  }
  reports {
    xml.required.set(false)
    html.required.set(false)
    txt.required.set(false)
    sarif.required.set(false)
  }
}

tasks.withType(io.gitlab.arturbosch.detekt.DetektCreateBaselineTask).configureEach {
  jvmTarget = "17"
}
